plugins {
  id 'java'
  id 'checkstyle'
  id 'org.springframework.boot' version '3.5.9'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.graalvm.buildtools.native' version '0.10.6'
  id 'org.cyclonedx.bom' version '2.3.1'
  id 'io.spring.javaformat' version '0.0.46'
  id "io.spring.nohttp" version "0.0.11"
}

gradle.startParameter.excludedTaskNames += [ "checkFormatAot", "checkFormatAotTest" ]

group = 'org.springframework.samples'
version = '3.5.9'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

repositories {
  mavenCentral()
}

ext.checkstyleVersion = "10.25.0"
ext.springJavaformatCheckstyleVersion = "0.0.46"
ext.webjarsLocatorLiteVersion = "1.1.0"
ext.webjarsFontawesomeVersion = "4.7.0"
ext.webjarsBootstrapVersion = "5.3.6"

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-cache'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'javax.cache:cache-api'
  implementation 'jakarta.xml.bind:jakarta.xml.bind-api'
  runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'
  runtimeOnly "org.webjars:webjars-locator-lite:${webjarsLocatorLiteVersion}"
  runtimeOnly "org.webjars.npm:bootstrap:${webjarsBootstrapVersion}"
  runtimeOnly "org.webjars.npm:font-awesome:${webjarsFontawesomeVersion}"
  runtimeOnly 'com.github.ben-manes.caffeine:caffeine'
  runtimeOnly 'com.h2database:h2'
  runtimeOnly 'com.mysql:mysql-connector-j'
  runtimeOnly 'org.postgresql:postgresql:42.7.7'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.boot:spring-boot-testcontainers'
  testImplementation 'org.springframework.boot:spring-boot-docker-compose'
  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'org.testcontainers:mysql'
  checkstyle "io.spring.javaformat:spring-javaformat-checkstyle:${springJavaformatCheckstyleVersion}"
  checkstyle "com.puppycrawl.tools:checkstyle:${checkstyleVersion}"
  // ✅ Add: bring in nohttp-checkstyle onto checkstyle classpath but exclude vulnerable transitives
  checkstyle("io.spring.nohttp:nohttp-checkstyle:0.0.11") {
    exclude group: "commons-beanutils", module: "commons-beanutils"
    exclude group: "com.google.guava", module: "guava"
    exclude group: "ch.qos.logback", module: "logback-classic"
    exclude group: "ch.qos.logback", module: "logback-core"
  }

  // ✅ Add: re-add fixed versions explicitly for the checkstyle toolchain
  checkstyle("commons-beanutils:commons-beanutils:1.11.0")
  checkstyle("com.google.guava:guava:33.4.8-jre")
  checkstyle("ch.qos.logback:logback-classic:1.5.19")
  checkstyle("ch.qos.logback:logback-core:1.5.19")

  // ✅ Fix Mend findings: checkstyle pulls these transitively
  checkstyle("org.apache.commons:commons-text:1.12.0")
  checkstyle("org.apache.commons:commons-lang3:3.18.0")

}
["checkstyle", "nohttp"].each { cfgName ->
  configurations.named(cfgName) {
    resolutionStrategy {
      force "org.apache.commons:commons-text:1.12.0"
      force "org.apache.commons:commons-lang3:3.18.0"
      force "commons-beanutils:commons-beanutils:1.11.0"
      force "com.google.guava:guava:33.4.8-jre"
      force "ch.qos.logback:logback-core:1.5.19"
      force "ch.qos.logback:logback-classic:1.5.19"
    }
  }
}

tasks.named('test') {
  useJUnitPlatform()
}

checkstyle {
  configDirectory = project.file('src/checkstyle')
  configFile = file('src/checkstyle/checkstyle.xml')
}

checkstyleNohttp {
  configDirectory = project.file('src/checkstyle')
  configFile = file('src/checkstyle/nohttp-checkstyle.xml')
}

tasks.named("formatMain").configure { dependsOn("checkstyleMain") }
tasks.named("formatMain").configure { dependsOn("checkstyleNohttp") }

tasks.named("formatTest").configure { dependsOn("checkstyleTest") }
tasks.named("formatTest").configure { dependsOn("checkstyleNohttp") }

checkstyleAot.enabled = false
checkstyleAotTest.enabled = false

checkFormatAot.enabled = false
checkFormatAotTest.enabled = false

formatAot.enabled = false
formatAotTest.enabled = false

wrapper {
  gradleVersion = "8.14.3"
  distributionType = Wrapper.DistributionType.ALL
}
